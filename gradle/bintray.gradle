def bintrayUser = project.hasProperty('bintrayUser') ? project.bintrayUser
        : System.getenv('BINTRAY_USER') ?: ''

def bintrayKey = project.hasProperty('bintrayKey') ? project.bintrayKey
        : System.getenv('BINTRAY_KEY') ?: ''

bintray {
    user = bintrayUser
    key = bintrayKey
    publish = true

    publications = ['mavenJava']

    pkg {
        repo = 'maven'
        name = 'commons'
        userOrg = 'fieldju'
        licenses = ['Apache 2.0']
        vcsUrl = 'https://github.com/fieldju/commons'
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar
        }
    }
}